################################################################################
# Copyright (C) 2014-2019 GSI Helmholtzzentrum fuer Schwerionenforschung GmbH  #
#                                                                              #
#              This software is distributed under the terms of the             #
#              GNU Lesser General Public Licence (LGPL) version 3,             #
#                  copied verbatim in the file "LICENSE"                       #
################################################################################

set(target ParBase)

set(sources
  FairContFact.cxx
  FairDetParAsciiFileIo.cxx
  FairDetParIo.cxx
  FairDetParRootFileIo.cxx
  FairGenericParAsciiFileIo.cxx
  FairGenericParRootFileIo.cxx
  FairParamList.cxx
  FairParAsciiFileIo.cxx
  FairParGenericSet.cxx
  FairParIo.cxx
  FairParRootFileIo.cxx
  FairParSet.cxx
  FairRtdbRun.cxx
  FairRuntimeDb.cxx
)

set(headers
  FairContFact.h
  FairDetParAsciiFileIo.h
  FairDetParIo.h
  FairDetParRootFileIo.h
  FairGenericParAsciiFileIo.h
  FairGenericParRootFileIo.h
  FairParamList.h
  FairParAsciiFileIo.h
  FairParGenericSet.h
  FairParIo.h
  FairParRootFileIo.h
  FairParSet.h
  FairRtdbRun.h
  FairRuntimeDb.h
)

add_library(${target} SHARED ${sources} ${headers})
add_library(FairRoot::${target} ALIAS ${target})
set_target_properties(${target} PROPERTIES ${FAIRROOT_LIBRARY_PROPERTIES})

target_include_directories(${target} PUBLIC
  $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
  $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>

  # TODO: DELETE ME ONCE USING root targets
  ${ROOT_INCLUDE_DIR}
)

target_link_directories(${target} PUBLIC
  ${ROOT_LIBRARY_DIR}
)

target_link_libraries(${target} PUBLIC
  FairRoot::FairTools
  # TODO: clean the following
  RIO
  Core
)

fairroot_target_root_dictionary(${target}
  HEADERS ${headers}
  LINKDEF ParBaseLinkDef.h
)

install(TARGETS ${target}
  LIBRARY DESTINATION ${PROJECT_INSTALL_LIBDIR}
)

install(FILES ${headers}
  DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)
